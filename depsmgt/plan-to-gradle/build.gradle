/*
	Target goal:
	1. rootProject is coffeeshop, this layer does not need src folder.
	2. For the sub-project part, *-web is the only part that needs to provide a web server, which will directly use the quarkus framework to provide rest api, and will depend on *-application (ex: orders-web depends on orders-application)
	3. In the sub-project, *-application is a service module, not a web application, and it will depend on its corresponding *-domain (ex: orders-application depends on orders-domain)
	4. For the sub-project, *-domain is the core of all business and will need to depend on ddd-commons
	5. Regarding the test, I will run quarkus test, specifically, you can try the unit test of orders-web
  */
buildscript {
    ext {
        quarkusJunitVersion = '0.22.0'
        restAssuredVersion = '3.3.0'
        cucumberVersion = '4.7.1'
        lombokVersion = '1.18.10'
        quarkusVersion = '0.23.1'
        awsJavaVersion = '1.11.631'
        awsVersion = '2.5.29'
    }
}

apply from: file("${rootDir}/gradle/project.gradle")

List testCompilePackage = ["io.quarkus:quarkus-junit5:${quarkusJunitVersion}", "io.rest-assured:rest-assured:${restAssuredVersion}"]
List testImplementPackage = ["io.cucumber:cucumber-java8:${cucumberVersion}", "io.cucumber:cucumber-junit:${cucumberVersion}"]
List implementationPackage = ["io.quarkus:quarkus-resteasy",
                              "com.amazonaws:aws-java-sdk-lambda",
                              "com.amazonaws:aws-java-sdk-dynamodb",
                              "com.amazonaws:aws-lambda-java-core",
                              "com.amazonaws:aws-lambda-java-events",
                              "com.amazonaws:aws-java-sdk-events"]

subprojects { dir ->
    repositories {
        mavenCentral()
    }

    dependencies {

        // Lombok Support
        compileOnly "org.projectlombok:lombok:${lombokVersion}"
        annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
        testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
        testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
        // quarkus test
        testCompile testCompilePackage
        // cucumber test
        testImplementation testImplementPackage
        // quarkus
        compile group: 'io.quarkus', name: 'quarkus-gradle-plugin', version: "${quarkusVersion}", ext: 'pom'
        implementation enforcedPlatform("io.quarkus:quarkus-bom:${quarkusVersion}")
        implementation platform("com.amazonaws:aws-java-sdk-bom:${awsJavaVersion}")
        implementation platform("software.amazon.awssdk:bom:${awsVersion}")
        implementation implementationPackage

    }

    if (dir.name.endsWith("-domain")) {
        dependencies {
            implementation project(":ddd-commons")
        }
    }

    if (dir.name.endsWith("-application")) {
        String modName = dir.name.substring(0, dir.name.lastIndexOf("-application"))
        dependencies {
            implementation project(":ddd-commons"), project(":${modName}-domain")
        }
    }

    if (dir.name.endsWith("-web")) {
        String modName = dir.name.substring(0, dir.name.lastIndexOf("-web"))
        dependencies {
            implementation project(":ddd-commons"), project(":${modName}-domain"), project(":${modName}-application")
        }
    }
}

